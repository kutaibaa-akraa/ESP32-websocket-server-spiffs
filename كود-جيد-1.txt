#include <WiFi.h>
#include <WebServer.h>
#include <WebSocketsServer.h>
#include <FS.h>
#include <SPIFFS.h>
#include <EEPROM.h>

#define EEPROM_SIZE 148

char ssid[32] = "D-uni";
char password[32] = "syfert122333444455555";
char static_ip[16] = "192.168.1.15";

char outputNames[4][16] = {"Output 1", "Output 2", "Output 3", "Output 4"};
bool outputStates[4] = {false, false, false, false};
unsigned long offTimers[4] = {0, 0, 0, 0};

WebServer server(80);
WebSocketsServer webSocket = WebSocketsServer(81);

const int outputs[] = {25, 26, 27, 14};

void loadNetworkSettings() {
  EEPROM.begin(EEPROM_SIZE);
  EEPROM.get(0, ssid);
  EEPROM.get(32, password);
  EEPROM.get(64, static_ip);
  for (int i = 0; i < 4; i++) {
    outputStates[i] = EEPROM.read(80 + i);
    EEPROM.get(84 + (i * 16), outputNames[i]);
  }
  EEPROM.end();
}

void saveNetworkSettings() {
  EEPROM.begin(EEPROM_SIZE);
  EEPROM.put(0, ssid);
  EEPROM.put(32, password);
  EEPROM.put(64, static_ip);
  for (int i = 0; i < 4; i++) {
    EEPROM.write(80 + i, outputStates[i]);
    EEPROM.put(84 + (i * 16), outputNames[i]);
  }
  EEPROM.commit();
  EEPROM.end();
}

void setupOutputs() {
  for (int i = 0; i < 4; i++) {
    pinMode(outputs[i], OUTPUT);
    digitalWrite(outputs[i], outputStates[i] ? HIGH : LOW);
  }
}

void handleRoot() {
  File file = SPIFFS.open("/index.html", "r");
  if (file) {
    server.streamFile(file, "text/html");
    file.close();
  } else {
    server.send(404, "text/plain", "File not found");
  }
}

void handleSaveSettings() {
  if (server.hasArg("ssid") && server.hasArg("pass") && server.hasArg("ip")) {
    strncpy(ssid, server.arg("ssid").c_str(), sizeof(ssid));
    strncpy(password, server.arg("pass").c_str(), sizeof(password));
    strncpy(static_ip, server.arg("ip").c_str(), sizeof(static_ip));
    for (int i = 0; i < 4; i++) {
      String argName = "name" + String(i);
      if (server.hasArg(argName)) {
        strncpy(outputNames[i], server.arg(argName).c_str(), sizeof(outputNames[i]));
      }
    }
    saveNetworkSettings();
    server.send(200, "text/plain", "Saved. Rebooting...");
    delay(1000);
    ESP.restart();
  } else {
    server.send(400, "text/plain", "Missing parameters");
  }
}

void handleApiOutput() {
  if (!server.hasArg("pin") || !server.hasArg("state")) {
    server.send(400, "application/json", "{\"error\":\"Missing pin or state\"}");
    return;
  }
  int pin = server.arg("pin").toInt();
  String state = server.arg("state");
  if (pin < 0 || pin > 3 || (state != "on" && state != "off")) {
    server.send(400, "application/json", "{\"error\":\"Invalid parameters\"}");
    return;
  }
  outputStates[pin] = (state == "on");
  digitalWrite(outputs[pin], outputStates[pin]);
  offTimers[pin] = 0;
  EEPROM.begin(EEPROM_SIZE);
  EEPROM.write(80 + pin, outputStates[pin]);
  EEPROM.commit();
  EEPROM.end();
  String msg = "{\"pin\":" + String(pin) + ",\"state\":\"" + state + "\"}";
  server.send(200, "application/json", msg);
  webSocket.broadcastTXT(String(pin) + ":" + (outputStates[pin] ? "1" : "0"));
}

void handleApiStatus() {
  String response = "{\"outputs\":[";
  for (int i = 0; i < 4; i++) {
    response += "{\"pin\":" + String(i) + ",\"state\":\"" + (outputStates[i] ? "on" : "off") + "\"}";
    if (i < 3) response += ",";
  }
  response += "]}";
  server.send(200, "application/json", response);
}

void webSocketEvent(uint8_t num, WStype_t type, uint8_t * payload, size_t length) {
  if (type == WStype_TEXT) {
    Serial.printf("WebSocket received: %s\n", payload);
    String data = String((char*)payload);
    if (data.indexOf(":") > 0) {
      int sep = data.indexOf(":");
      int pinIndex = data.substring(0, sep).toInt();
      int seconds = data.substring(sep + 1).toInt();
      if (pinIndex >= 0 && pinIndex < 4) {
        outputStates[pinIndex] = true;
        digitalWrite(outputs[pinIndex], HIGH);
        offTimers[pinIndex] = millis() + (seconds * 1000);
        EEPROM.begin(EEPROM_SIZE);
        EEPROM.write(80 + pinIndex, outputStates[pinIndex]);
        EEPROM.commit();
        EEPROM.end();
        String message = String(pinIndex) + ":1";
        webSocket.broadcastTXT(message);
      }
    } else {
      int pinIndex = data.toInt();
      if (pinIndex >= 0 && pinIndex < 4) {
        outputStates[pinIndex] = !outputStates[pinIndex];
        digitalWrite(outputs[pinIndex], outputStates[pinIndex]);
        offTimers[pinIndex] = 0;
        EEPROM.begin(EEPROM_SIZE);
        EEPROM.write(80 + pinIndex, outputStates[pinIndex]);
        EEPROM.commit();
        EEPROM.end();
        String message = String(pinIndex) + ":" + (outputStates[pinIndex] ? "1" : "0");
        webSocket.broadcastTXT(message);
      }
    }
  }
}

void setup() {
  Serial.begin(115200);
  loadNetworkSettings();

  IPAddress localIP;
  localIP.fromString(static_ip);
  IPAddress gateway(192,168,1,1);
  IPAddress subnet(255,255,255,0);
  WiFi.config(localIP, gateway, subnet);
  WiFi.begin(ssid, password);

  Serial.print("Connecting to WiFi");
  unsigned long startAttemptTime = millis();
  while (WiFi.status() != WL_CONNECTED && millis() - startAttemptTime < 10000) {
    delay(500);
    Serial.print(".");
  }

  if (WiFi.status() != WL_CONNECTED) {
    Serial.println("\nFailed to connect, starting Access Point...");
    WiFi.softAP("ESP32_SETUP", "12345678");
    Serial.println("AP IP address: " + WiFi.softAPIP().toString());
  } else {
    Serial.println("\nConnected!");
    Serial.println(WiFi.localIP());
  }

  if (!SPIFFS.begin(true)) {
    Serial.println("SPIFFS mount failed");
    return;
  }

  setupOutputs();

  server.on("/", handleRoot);
  server.on("/save", HTTP_POST, handleSaveSettings);
  server.on("/api/output", handleApiOutput);
  server.on("/api/status", handleApiStatus);
  server.begin();

  webSocket.begin();
  webSocket.onEvent(webSocketEvent);
}

void loop() {
  server.handleClient();
  webSocket.loop();
  for (int i = 0; i < 4; i++) {
    if (offTimers[i] > 0 && millis() > offTimers[i]) {
      outputStates[i] = false;
      digitalWrite(outputs[i], LOW);
      offTimers[i] = 0;
      EEPROM.begin(EEPROM_SIZE);
      EEPROM.write(80 + i, 0);
      EEPROM.commit();
      EEPROM.end();
      String message = String(i) + ":0";
      webSocket.broadcastTXT(message);
    }
  }
}
